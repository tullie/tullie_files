#

function prompt_skwp_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git or Hg repository information.
  if (( $+functions[git-info] )); then
    vc_info="$(_dotfiles_scm_info)"
  fi
}

function prompt_tullie_fb_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_skwp_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_TULLIE_fb_COLORS=(
      "%F{81}"  # turquoise
      "%F{227}" # yellow
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_TULLIE_fb_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  PROMPT="${__PROMPT_TULLIE_fb_COLORS[1]}%n%f ${__PROMPT_TULLIE_fb_COLORS[2]}%~%f $ "
  RPROMPT='$vc_info'
}

prompt_tullie_fb_setup "$@"
